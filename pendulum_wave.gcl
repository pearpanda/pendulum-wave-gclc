%%% Petar Nikić - Talasno Klatno

%% Konstante

% g
number g 9.81

% pi
number pi 3.141592653589793

array pi_frac { 12 }
number n 1
while { n <= 12 }
{
    expression pi_frac[n] { pi / n }
    expression n { n + 1 }
}

%% Konfiguracija

number BrojKlatana 6
number BrojPerioda 1.2
number GlobalniPeriod 40
number BrojOscilacijaNajduzeg 20
number Rastojanje 1
number FPS 24

number UgaoAmplituda pi_frac[6]

%% Izvođenja

expression frames { BrojPerioda * GlobalniPeriod * FPS }  % 1152
point Point_Vreme 0 0 1152 0

expression dt { 1 / FPS }

array BrojOscilacija { BrojKlatana }
array Period { BrojKlatana }
array Duzina { BrojKlatana }

number n 0
while { n < BrojKlatana }
{
    expression BrojOscilacija[n] { BrojOscilacijaNajduzeg + n }
    expression Period[n] { GlobalniPeriod / BrojOscilacija[n] }
    expression Duzina[n] { g * pow(Period[n] / (2 * pi), 2) }    % Aproksimacija malih uglova
    expression n { n + 1 }
}

%% Simulacija - Ojler-Kromerova metoda

% Vreme
getx t_Simulacije Point_Vreme

% Simulacija
procedure KlatnoOjlerKromer { Iteracija Ugao Brzina Duzina g dt }
{
    number t 1
    while { t < Iteracija }
    {
        expression Ubrzanje { - (g / Duzina) * sin(Ugao) }
        expression Brzina { Brzina + Ubrzanje * dt }
        expression Ugao { Ugao + Brzina * dt }
        expression t { t + 1 }
    }
}

%% Crtanje - 3D
number X 240
number Y 160

expression X_C { X / 2 }
expression Y_C { Y / 2 }
expression Y_T { Y - (Y / 10) }

dim X Y
point O_z 0 0
point O X_C Y_T

number k 0
while { k < BrojKlatana }
{
    number Ugao UgaoAmplituda
    number Brzina 0

    call KlatnoOjlerKromer { t_Simulacije Ugao Brzina Duzina[k] g dt }
    expression UgaoDeg { (Ugao / pi) * 180 }

    expression MinY { - Y * (Duzina[k] / 1.8) }
    point A_zs 0 MinY
    rotate A_z O_z UgaoDeg A_zs
    translate A O_z O A_z

    drawsegment O A
    cmark_lb A
    cmark_rt O

    expression k { k + 1 }
}

animation_frames frames FPS
